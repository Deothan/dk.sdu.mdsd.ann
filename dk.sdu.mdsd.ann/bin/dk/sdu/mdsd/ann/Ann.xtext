grammar dk.sdu.mdsd.ann.Ann with org.eclipse.xtext.common.Terminals

generate ann "http://www.sdu.dk/mdsd/ann/Ann"


ANNModel:
	name=ID '{' 
		'alpha' alpha=DECIMAL 
		'epochs' epochs=INT 
		constraints+=Constraint*
		Activation+=Activation*
		Layer+=Layer* '}'
;    

DECIMAL:
 INT '.' INT
;

Layer:
	Hidden | Input | Output
;
	
Hidden:
	'hidden' '{' 
		'size' size=INT
		'activation' l_rule= [Activation] '}' 
;

Input:
	'in' '{' 
		'size' size=INT 
		'source' source=STRING
		('constraints' restrictions=[Constraint]? moreRestrictions+=Restriction*)?  '}' 
;

Restriction:
	',' constraint=[Constraint]
;

Constraint:
	'constraint' name=ID '(' field=INT fields+=Field* ')' '{'
		condition = Condition
	'}'		
;

Field:
	',' field=INT
;

Condition:
	condition=ConditionalOperator num2=NumberTypes
;

ConditionalOperator:
 '==' | '<' | '>' | '<=' | '>='
;


Output:
	'out' '{' 
		'size' size=INT
		'activation' l_rule= [Activation]'}' 
;

Activation:
	Sigmoid | Tansig | External | Custom
;

External:
	name=ID 'external'
;

Sigmoid:
	name=ID rule='sigmoid'
;

Tansig:
	name=ID rule='tansig'
;

Custom:
	name=ID '(x)''{'
	'function' '=' exp=Expression
	'derivative' '=' der=Expression '}'
;

Expression:
	Factor ({Part.left = current} operator = PartOperator right = Factor)*
;

Factor returns Expression:
	Primary ({Fac.left = current} operator = FactorOperator right = Primary)*
;

Primary returns Expression:
	NumberLiteral | Letter | '(' Expression ')' | Math
;

Math:
	Euler | NLog | Sin | Cos | Sqrt | Power
;

Euler:
	'e(' value = Expression ')'
;

NLog:
	'nlog(' value = Expression ')'
;

Sin:
	'sin(' value = Expression ')'
;

Cos:
	'cos(' value = Expression ')'
;

Sqrt:
	'sqrt(' value = Expression ')'
;

Power:
	'pow('base = Expression ',' exponent = Expression ')'
;

Letter:
	operator = PartOperator?
	value = "x"
;

PartOperator:
	'+' | '-'
;

FactorOperator:
	'*' | '/'
;

NumberLiteral:
	operator = PartOperator?
	value = NumberTypes
;

NumberTypes:
	DECIMAL | INT
;