grammar dk.sdu.mdsd.ann.Ann with org.eclipse.xtext.common.Terminals

generate ann "http://www.sdu.dk/mdsd/ann/Ann"


ANNModel:
		name=ID '{' 
			'alpha' alpha=DECIMAL 
			'epochs' epochs=INT 
			Activation+=Activation*
			Layer+=Layer* '}'
;    

DECIMAL:
 INT '.' INT
;

Layer:
	Hidden | Input | Output
;
	
Hidden:
	'hidden' '{' 
		'size' size=INT
		'activation' l_rule= [Activation] '}' 
;

Input:
	'in' '{' 
		'size' size=INT '}' 
;

Output:
	'out' '{' 
		'size' size=INT
		'activation' l_rule= [Activation]'}' 
;

Activation:
	Sigmoid | Tansig | External | Custom
;

Custom:
	name=ID '(x)''{'
	'function' '=' exp=Expression
	'derivative' '=' der=Expression '}'
;

External:
	name=ID 'external'
;

Sigmoid:
	name=ID rule='sigmoid'
;

Tansig:
	name=ID rule='tansig'
;

Expression:
	Substraction ({Add.left = current} '+' right = Substraction)*
;

Substraction returns Expression:
	Multiplication ({Sub.left = current} '-' right = Multiplication)*
;

Multiplication returns Expression:
	Division ({Multi.left = current} '*' right = Division)*
;

Division returns Expression:
	Primary ({Div.left = current} '/' right = Primary)*
;

Primary returns Expression:
	NumberLiteral | Letter | '(' Expression ')'
;

Letter:
	value = "x"
;

NumberLiteral:
	value = DECIMAL
;